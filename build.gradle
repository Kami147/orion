buildscript {
	ext.kotlin_version = "1.3.61"
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
		maven {
			name = 'SpongePowered'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
	}
}

apply plugin: "kotlin"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.github.johnrengelman.shadow"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
	version = project.forgeVersion
	runDir = "run"
	mappings = project.mcpVersion
	coreMod = "git.littledraily.orion.mixin.MixinLoader"
	makeObfSourceJar = false
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven {
		name = 'spongepowered-repo'
		url = 'http://repo.spongepowered.org/maven/'
	}
}

dependencies {
	compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
		exclude module: 'commons-io'
	}
	compile(group: 'org.reflections', name: 'reflections', version: '0.9.11') {
		exclude group: 'com.google.guava', module: 'guava'
	}
	compile 'com.github.LittleDraily:event-system:2.0'
	compile 'org.slick2d:slick2d-core:1.0.2'
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

}

//jar {
//	manifest {
//		attributes(
//				'MixinConfigs': 'mixins.orion.json',
//				'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
//				'TweakOrder': 0,
//				'FMLCorePluginContainsFMLMod': 'true',
//				'FMLCorePlugin': 'git.littledraily.orion.mixin.MixinLoader',
//				'ForceLoadAsMod': 'true',
//				'FMLAT': 'orion_at.cfg'
//		)
//	}
//}

mixin {
	defaultObfuscationEnv searge
	add sourceSets.main, 'mixins.orion.refmap.json'
}

reobf {
	shadowJar {
		mappingType = 'SEARGE'
		classpath = sourceSets.main.compileClasspath
	}
}

shadowJar {
	dependencies {
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk7"))
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8"))
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib-common"))
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib"))
		include(dependency('com.github.LittleDraily:event-system:2.0'))
		include(dependency('org.spongepowered:mixin'))
		include(dependency('org.reflections:reflections'))
		include(dependency('org.slick2d:slick2d-core:1.0.2'))
	}
	exclude 'dummyThing'
	exclude 'LICENSE.txt'
	classifier = 'release'
}

build.dependsOn(shadowJar)

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

sourceSets {
	main.java.srcDirs = ["src/main/java", "src/main/kotlin"]
	main.resources.srcDirs = ["src/main/resources"]
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}
